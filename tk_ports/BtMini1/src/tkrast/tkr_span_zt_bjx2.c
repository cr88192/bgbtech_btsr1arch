#ifdef __BJX2__

void TKRA_DrawSpan_ModTexZt(u64 *parm,
	tkra_rastpixel *dstc, tkra_zbufpixel *dstz, int cnt);
void TKRA_DrawSpan_ModTexMortZt(u64 *parm,
	tkra_rastpixel *dstc, tkra_zbufpixel *dstz, int cnt);
void TKRA_DrawSpan_AlphaModTexMortZt(u64 *parm,
	tkra_rastpixel *dstc, tkra_zbufpixel *dstz, int cnt);
void TKRA_DrawSpan_AtestModTexMortZt(u64 *parm,
	tkra_rastpixel *dstc, tkra_zbufpixel *dstz, int cnt);

void TKRA_DrawSpan_ModBlTexMortZt(u64 *parm,
	tkra_rastpixel *dstc, tkra_zbufpixel *dstz, int cnt);
void TKRA_DrawSpan_AlphaModBlTexMortZt(u64 *parm,
	tkra_rastpixel *dstc, tkra_zbufpixel *dstz, int cnt);
void TKRA_DrawSpan_AtestModBlTexMortZt(u64 *parm,
	tkra_rastpixel *dstc, tkra_zbufpixel *dstz, int cnt);

void TKRA_DrawSpan_ModUtx2MortZt(u64 *parm,
	tkra_rastpixel *dstc, tkra_zbufpixel *dstz, int cnt);
void TKRA_DrawSpan_ModUtx2MortZb(u64 *parm,
	tkra_rastpixel *dstc, tkra_zbufpixel *dstz, int cnt);

void TKRA_DrawSpan_AlphaModUtx2MortZt(u64 *parm,
	tkra_rastpixel *dstc, tkra_zbufpixel *dstz, int cnt);
void TKRA_DrawSpan_AlphaModUtx2MortZb(u64 *parm,
	tkra_rastpixel *dstc, tkra_zbufpixel *dstz, int cnt);
void TKRA_DrawSpan_AtestModUtx2MortZt(u64 *parm,
	tkra_rastpixel *dstc, tkra_zbufpixel *dstz, int cnt);
void TKRA_DrawSpan_AtestModUtx2MortZb(u64 *parm,
	tkra_rastpixel *dstc, tkra_zbufpixel *dstz, int cnt);

void TKRA_DrawSpan_ModBlUtx2MortZt(u64 *parm,
	tkra_rastpixel *dstc, tkra_zbufpixel *dstz, int cnt);
void TKRA_DrawSpan_ModBlUtx2MortZb(u64 *parm,
	tkra_rastpixel *dstc, tkra_zbufpixel *dstz, int cnt);

void TKRA_DrawSpan_AlphaModBlUtx2MortZt(u64 *parm,
	tkra_rastpixel *dstc, tkra_zbufpixel *dstz, int cnt);
void TKRA_DrawSpan_AlphaModBlUtx2MortZb(u64 *parm,
	tkra_rastpixel *dstc, tkra_zbufpixel *dstz, int cnt);
void TKRA_DrawSpan_AtestModBlUtx2MortZt(u64 *parm,
	tkra_rastpixel *dstc, tkra_zbufpixel *dstz, int cnt);
void TKRA_DrawSpan_AtestModBlUtx2MortZb(u64 *parm,
	tkra_rastpixel *dstc, tkra_zbufpixel *dstz, int cnt);

void TKRA_DrawSpan_Zbuf(tkra_zbufpixel *dstz, int cnt, u64 zpos, u64 zstep);
void TKRA_DrawSpan_ZbNul(tkra_zbufpixel *dstz, int cnt, u64 zpos, u64 zstep);


__asm {
TKRA_DrawSpan_ModTexZt:
	ADD		-64, SP
	MOV.X	R30, (SP, 48)
	MOV.X	R28, (SP, 32)
	MOV.X	R26, (SP, 16)
	MOV.X	R24, (SP,  0)

	MOV.Q	(R4, TKRA_DS_TEXIMG*8), R20

	MOV.Q	(R4, TKRA_DS_ZPOS *8), R24
	MOV.Q	(R4, TKRA_DS_ZSTEP*8), R25
	MOV.Q	(R4, TKRA_DS_CPOS *8), R26
	MOV.Q	(R4, TKRA_DS_CSTEP*8), R27
	MOV.Q	(R4, TKRA_DS_TPOS *8), R28
	MOV.Q	(R4, TKRA_DS_TSTEP*8), R29
	MOV.Q	(R4, TKRA_DS_XMASK*8), R30
	MOV.Q	(R4, TKRA_DS_YMASK*8), R31
	LEA.W	(R5, R7), R21

	.L0:	
	SHLD.Q		R28, -16, R3	|	SHLD.Q		R28, -32, R2
	AND			R3, R30, R3		|	AND			R2, R31, R2
	ADD			R29, R28		|	OR			R2, R3
	MOV.W		(R20, R3), R2
	RGB5UPCK64	R2, R2
	PMULU.HW	R2, R26, R2
	ADD			R27, R26		| RGB5PCK64		R2, R2
	SHAD		R24, -16, R17	| MOV.W		(R6), R16
	ADD			R25, R24		| CMPHI			R17, R16
	MOV.W?T		R2, (R5)
//	MOV.W?T		R17, (R6)
	ADD			2, R5			| ADD			2, R6
	CMPGT		R5, R21
	BT			.L0

	MOV.X	(SP,  0), R24
	MOV.X	(SP, 16), R26
	MOV.X	(SP, 32), R28
	MOV.X	(SP, 48), R30
	ADD		64, SP
	RTSU

TKRA_DrawSpan_ModTexMortZt:
	ADD		-64, SP
	MOV.X	R30, (SP, 48)
	MOV.X	R28, (SP, 32)
	MOV.X	R26, (SP, 16)
	MOV.X	R24, (SP,  0)

	MOV.Q	(R4, TKRA_DS_TEXIMG*8), R20
	MOV.X	(R4, TKRA_DS_ZPOS *8), R24
	MOV.X	(R4, TKRA_DS_CPOS *8), R26
	MOV.X	(R4, TKRA_DS_TPOS *8), R28
	MOV.Q	(R4, TKRA_DS_YMASK*8), R31
	LEA.W	(R5, R7), R21

	.L0:	
//	SHLD.Q		R28, -16, R3	|	SHLD.Q		R28, -32, R2
//	AND			R3, R30, R3		|	AND			R2, R31, R2
	PMORT.L		R28, R2
	ADD			R29, R28		|	AND			R2, R31, R3
	MOV.W		(R20, R3), R2
	RGB5UPCK64	R2, R2
	PMULU.HW	R2, R26, R2
	ADD			R27, R26		| RGB5PCK64		R2, R2
	SHAD		R24, -16, R17	| MOV.W		(R6), R16
	ADD			R25, R24

//	CMPHI			R17, R16
//	MOV.W?T		R2, (R5)
//	MOV.W?T		R17, (R6)

	CMPGT		R16, R17
	MOV.W?F		R2, (R5)
//	MOV.W?F		R17, (R6)

	ADD			2, R5			| ADD			2, R6
	CMPGT		R5, R21
	BT			.L0

	MOV.X	(SP,  0), R24
	MOV.X	(SP, 16), R26
	MOV.X	(SP, 32), R28
	MOV.X	(SP, 48), R30
	ADD		64, SP
	RTSU


TKRA_DrawSpan_ModTexMortZb:
	ADD		-64, SP
	MOV.X	R30, (SP, 48)
	MOV.X	R28, (SP, 32)
	MOV.X	R26, (SP, 16)
	MOV.X	R24, (SP,  0)

	MOV.Q	(R4, TKRA_DS_TEXIMG*8), R20
	MOV.X	(R4, TKRA_DS_ZPOS *8), R24
	MOV.X	(R4, TKRA_DS_CPOS *8), R26
	MOV.X	(R4, TKRA_DS_TPOS *8), R28
	MOV.Q	(R4, TKRA_DS_YMASK*8), R31
	LEA.W	(R5, R7), R21

	.L0:	
	PMORT.L		R28, R2
	ADD			R29, R28		|	AND			R2, R31, R3
	MOV.W		(R20, R3), R2
	RGB5UPCK64	R2, R2
	PMULU.HW	R2, R26, R2
	ADD			R27, R26		| RGB5PCK64		R2, R2
	SHAD		R24, -16, R17	| MOV.W		(R6), R16
	ADD			R25, R24

	CMPGT		R16, R17
	MOV.W?F		R2, (R5)
	MOV.W?F		R17, (R6)

	ADD			2, R5			| ADD			2, R6
	CMPGT		R5, R21
	BT			.L0

	MOV.X	(SP,  0), R24
	MOV.X	(SP, 16), R26
	MOV.X	(SP, 32), R28
	MOV.X	(SP, 48), R30
	ADD		64, SP
	RTSU

TKRA_DrawSpan_AlphaModTexMortZt:
	ADD		-64, SP
	MOV.X	R30, (SP, 48)
	MOV.X	R28, (SP, 32)
	MOV.X	R26, (SP, 16)
	MOV.X	R24, (SP,  0)
	
	MOV.Q	(R4, TKRA_DS_TEXIMG*8), R20
	MOV.X	(R4, TKRA_DS_ZPOS *8), R24
	MOV.X	(R4, TKRA_DS_CPOS *8), R26
	MOV.X	(R4, TKRA_DS_TPOS *8), R28
	MOV.Q	(R4, TKRA_DS_YMASK*8), R31
	LEA.W	(R5, R7), R21

/*
R2: Scratch
R3: Scratch
R4: Span State
R5: Dest Pixel
R6: Dest ZBuf
R7: Count
R8: Scratch
R9: Scratch
R10: Scratch
R11: Scratch
R12: Scratch
R13: Scratch
R14: Scratch
R15: SP
R16: Scratch
R17: Scratch / ZPos
R18: Pixel A / TexPos A
R19: Pixel B / TexPos B
R20: Texture Pixels
R21: Span End Address
R22: Scratch / Next Pointer
R23: Scratch
R24: ZPos
R25: ZStep
R26: CPos
R27: CStep
R28: TPos
R29: TStep
R30: -
R31: X/Y Mask
 */

	.L1:
//	CMPQGT		R5, R21
//	BF			.L2

	PMORT.L		R28, R18		|	MOVU.W		(R5), R19
	ADD			R29, R28		|	AND			R18, R31, R3
									MOV.W		(R20, R3), R18
//	RGB5UPCK64	R18, R18		|	PSHUF.W		R26, 0xFF, R22
//	RGB5UPCK64	R19, R19		|	NOT			R22, R23
	RGB5UPCK64	R18, R18		|	RGB5UPCK64	R19, R19
	PMULU.HW	R18, R26, R18
	PSHUF.W		R18, 0xFF, R22
	NOT			R22, R23
	PMULU.HW	R18, R22, R2	|	PMULU.HW	R19, R23, R3
	SHAD.Q		R24, -16, R17	|	PADD.W		R2, R3, R18
	ADD			R27, R26		|	RGB5PCK64	R18, R18
	ADD			R25, R24		|	MOV.W		(R6), R16
									CMPGT		R16, R17
									MOV.W?F		R18, (R5)
	ADD			2, R5			|	ADD			2, R6
	CMPGT		R5, R21
	BT			.L1

	.L2:

	MOV.X	(SP,  0), R24
	MOV.X	(SP, 16), R26
	MOV.X	(SP, 32), R28
	MOV.X	(SP, 48), R30
	ADD		64, SP
	RTSU


TKRA_DrawSpan_ModBlTexMortZt:
	ADD		-128, SP
	MOV.X	R30, (SP, 112)
	MOV.X	R28, (SP,  96)
	MOV.X	R26, (SP,  80)
	MOV.X	R24, (SP,  64)
	MOV.Q	R14, (SP,  48)
	MOV.X	R12, (SP,  32)
	MOV.X	R10, (SP,  16)
	MOV.X	R8 , (SP,   0)

	MOV.Q	(R4, TKRA_DS_TEXIMG*8), R20
	MOV.X	(R4, TKRA_DS_ZPOS *8), R24
	MOV.X	(R4, TKRA_DS_CPOS *8), R26
	MOV.X	(R4, TKRA_DS_TPOS *8), R28
	MOV.Q	(R4, TKRA_DS_YMASK*8), R31
	LEA.W	(R5, R7), R21

	.L1:
	MOV			0x00000000FFFF, R8
	MOV			0xFFFF00000000, R9
	ADD			R28, R8, R17	|	PMORT.L		R28, R18
	AND			R18, R31, R2	|	PMORT.L		R17, R19
	AND			R19, R31, R3	|	MOV.W		(R20, R2), R18
	ADD			R28, R9, R16	|	MOV.W		(R20, R3), R19
	ADD			R16, R8, R17	|	PMORT.L		R16, R22
	AND			R22, R31, R22	|	PMORT.L		R17, R23
	AND			R23, R31, R23	|	MOV.W		(R20, R22), R10
	MOV			R28, R16		|	MOV.W		(R20, R23), R11
	ADD			R29, R28		|	PSHUF.W		R16, 0, R22
	RGB5UPCK64	R18, R8			|	RGB5UPCK64	R19, R9
	NOT			R22, R23		|	PSHUF.W		R16, 0xAA, R18
	NOT			R18, R19		|
	RGB5UPCK64	R10, R10		|	RGB5UPCK64	R11, R11
	PMULU.HW	R8, R23, R12	|	PMULU.HW	R9, R22, R13
	PMULU.HW	R10, R23, R14	|	PMULU.HW	R11, R22, R30
	PADD.W		R12, R13, R8	|	PADD.W		R14, R30, R9
	PMULU.HW	R8, R19, R12	|	PMULU.HW	R9, R18, R13
									PADD.W		R12, R13, R18
									PMULU.HW	R18, R26, R18
	ADD			R27, R26		|	RGB5PCK64	R18, R18
	SHAD.Q		R24, -16, R17	|	MOV.W		(R6), R16
	ADD			R25, R24		|	CMPGT		R16, R17
									MOV.W?F		R18, (R5)
	ADD			2, R5			|	ADD			2, R6

	CMPGT		R5, R21
	BT			.L1

	.L2:

	MOV.X	(SP,   0), R8
	MOV.X	(SP,  16), R10
	MOV.X	(SP,  32), R12
	MOV.Q	(SP,  48), R14
	MOV.X	(SP,  64), R24
	MOV.X	(SP,  80), R26
	MOV.X	(SP,  96), R28
	MOV.X	(SP, 112), R30
	ADD		128, SP
	RTSU

TKRA_DrawSpan_AlphaModBlTexMortZt:
	ADD		-128, SP
	MOV.X	R30, (SP, 112)
	MOV.X	R28, (SP,  96)
	MOV.X	R26, (SP,  80)
	MOV.X	R24, (SP,  64)
	MOV.Q	R14, (SP,  48)
	MOV.X	R12, (SP,  32)
	MOV.X	R10, (SP,  16)
	MOV.X	R8 , (SP,   0)

	MOV.Q	(R4, TKRA_DS_TEXIMG*8), R20
	MOV.X	(R4, TKRA_DS_ZPOS *8), R24
	MOV.X	(R4, TKRA_DS_CPOS *8), R26
	MOV.X	(R4, TKRA_DS_TPOS *8), R28
	MOV.Q	(R4, TKRA_DS_YMASK*8), R31
	LEA.W	(R5, R7), R21

	.L1:
	MOV			0x00000000FFFF, R8
	MOV			0xFFFF00000000, R9
	ADD			R28, R8, R17	|	PMORT.L		R28, R18
	AND			R18, R31, R2	|	PMORT.L		R17, R19
	AND			R19, R31, R3	|	MOV.W		(R20, R2), R18
	ADD			R28, R9, R16	|	MOV.W		(R20, R3), R19
	ADD			R16, R8, R17	|	PMORT.L		R16, R22
	AND			R22, R31, R22	|	PMORT.L		R17, R23
	AND			R23, R31, R23	|	MOV.W		(R20, R22), R10
	MOV			R28, R16		|	MOV.W		(R20, R23), R11
	ADD			R29, R28		|	PSHUF.W		R16, 0, R22
	RGB5UPCK64	R18, R8			|	RGB5UPCK64	R19, R9
	NOT			R22, R23		|	PSHUF.W		R16, 0xAA, R18
	NOT			R18, R19		|
	RGB5UPCK64	R10, R10		|	RGB5UPCK64	R11, R11
	PMULU.HW	R8, R23, R12	|	PMULU.HW	R9, R22, R13
	PMULU.HW	R10, R23, R14	|	PMULU.HW	R11, R22, R30
	PADD.W		R12, R13, R8	|	PADD.W		R14, R30, R9
	PMULU.HW	R8, R19, R12	|	PMULU.HW	R9, R18, R13
	PADD.W		R12, R13, R18	|	MOV.W		(R5), R19
									PMULU.HW	R18, R26, R18
									PSHUF.W		R18, 0xFF, R22
	RGB5UPCK64	R19, R19		|	NOT			R22, R23
	PMULU.HW	R18, R22, R12	|	PMULU.HW	R19, R23, R13
	ADD			R27, R26		|	PADD.W		R12, R13, R18
									RGB5PCK64	R18, R18
	SHAD.Q		R24, -16, R17	|	MOV.W		(R6), R16
	ADD			R25, R24		|	CMPGT		R16, R17
									MOV.W?F		R18, (R5)
	ADD			2, R5			|	ADD			2, R6
	CMPGT		R5, R21
	BT			.L1

	.L2:

	MOV.X	(SP,   0), R8
	MOV.X	(SP,  16), R10
	MOV.X	(SP,  32), R12
	MOV.Q	(SP,  48), R14
	MOV.X	(SP,  64), R24
	MOV.X	(SP,  80), R26
	MOV.X	(SP,  96), R28
	MOV.X	(SP, 112), R30
	ADD		128, SP
	RTSU



TKRA_DrawSpan_ModUtx2MortZt:
	ADD		-64, SP
	MOV.X	R30, (SP, 48)
	MOV.X	R28, (SP, 32)
	MOV.X	R26, (SP, 16)
	MOV.X	R24, (SP,  0)

	MOV.Q	(R4, TKRA_DS_TEXBCN*8), R20

	MOV.X	(R4, TKRA_DS_ZPOS *8), R24
	MOV.X	(R4, TKRA_DS_CPOS *8), R26
	MOV.X	(R4, TKRA_DS_TPOS *8), R28

//	MOV.Q	(R4, TKRA_DS_ZPOS *8), R24
//	MOV.Q	(R4, TKRA_DS_ZSTEP*8), R25
//	MOV.Q	(R4, TKRA_DS_CPOS *8), R26
//	MOV.Q	(R4, TKRA_DS_CSTEP*8), R27
//	MOV.Q	(R4, TKRA_DS_TPOS *8), R28
//	MOV.Q	(R4, TKRA_DS_TSTEP*8), R29
//	MOV.Q	(R4, TKRA_DS_XMASK*8), R30
	MOV.Q	(R4, TKRA_DS_YMASK*8), R31
	LEA.W	(R5, R7), R21

	.L0:	
									PMORT.L		R28, R2
	ADD			R29, R28		|	AND			R2, R31, R3
	SHAD		R3, -4, R2		|	MOV.W		(R6), R16
	SHAD		R24, -16, R17	|	MOV.Q		(R20, R2), R2
									BLKUTX2		R2, R3, R2
									PMULU.HW	R2, R26, R2
	ADD			R27, R26		|	RGB5PCK64	R2, R2
	ADD			R25, R24		|	CMPGT		R16, R17
									MOV.W?F		R2, (R5)
	ADD			2, R5			|	ADD			2, R6

	CMPGT		R5, R21
	BT			.L0

	MOV.X	(SP,  0), R24
	MOV.X	(SP, 16), R26
	MOV.X	(SP, 32), R28
	MOV.X	(SP, 48), R30
	ADD		64, SP
	RTSU

#if 0
TKRA_DrawSpan_ModUtx2MortZb:
	ADD		-64, SP
	MOV.X	R30, (SP, 48)
	MOV.X	R28, (SP, 32)
	MOV.X	R26, (SP, 16)
	MOV.X	R24, (SP,  0)

	MOV.Q	(R4, TKRA_DS_TEXBCN*8), R20

	MOV.X	(R4, TKRA_DS_ZPOS *8), R24
	MOV.X	(R4, TKRA_DS_CPOS *8), R26
	MOV.X	(R4, TKRA_DS_TPOS *8), R28

//	MOV.Q	(R4, TKRA_DS_ZPOS *8), R24
//	MOV.Q	(R4, TKRA_DS_ZSTEP*8), R25
//	MOV.Q	(R4, TKRA_DS_CPOS *8), R26
//	MOV.Q	(R4, TKRA_DS_CSTEP*8), R27
//	MOV.Q	(R4, TKRA_DS_TPOS *8), R28
//	MOV.Q	(R4, TKRA_DS_TSTEP*8), R29
//	MOV.Q	(R4, TKRA_DS_XMASK*8), R30
	MOV.Q	(R4, TKRA_DS_YMASK*8), R31
	LEA.W	(R5, R7), R21

	.L0:	
									PMORT.L		R28, R2
	ADD			R29, R28		|	AND			R2, R31, R3
	SHAD		R3, -4, R2		|	MOV.W		(R6), R16
	SHAD		R24, -16, R17	|	MOV.Q		(R20, R2), R2
									BLKUTX2		R2, R3, R2
									PMULU.HW	R2, R26, R2
	ADD			R27, R26		|	RGB5PCK64	R2, R2
	ADD			R25, R24		|	CMPGT		R16, R17
									MOV.W?F		R2, (R5)
									MOV.W?F		R17, (R6)
	ADD			2, R5			|	ADD			2, R6

	CMPGT		R5, R21
	BT			.L0

	MOV.X	(SP,  0), R24
	MOV.X	(SP, 16), R26
	MOV.X	(SP, 32), R28
	MOV.X	(SP, 48), R30
	ADD		64, SP
	RTSU
#endif

#if 0
TKRA_DrawSpan_ModUtx2MortZb:
	ADD		-64, SP
	MOV.X	R30, (SP, 48)
	MOV.X	R28, (SP, 32)
	MOV.X	R26, (SP, 16)
	MOV.X	R24, (SP,  0)

	MOV.Q	(R4, TKRA_DS_TEXBCN*8), R20

	MOV.X	(R4, TKRA_DS_ZPOS *8), R24
	MOV.X	(R4, TKRA_DS_CPOS *8), R26
	MOV.X	(R4, TKRA_DS_TPOS *8), R28
//	MOV.Q	(R4, TKRA_DS_XMASK*8), R30
	MOV.Q	(R4, TKRA_DS_YMASK*8), R31
	LEA.W	(R5, R7), R21
	PMORT.L		R28, R18
	AND			R18, R31, R19
//	SHAD		R19, -4, R21
	SHAD		R19, -4, R3

	.L0:	
	ADD			R29, R28		|	MOV.Q		(R20, R3), R2
	SHAD		R24, -16, R17	|	MOV.W		(R6), R16
	PMORT.L		R28, R18		|	BLKUTX2		R2, R19, R2
	ADD			R25, R24		|	PMULU.HW	R2, R26, R2
	AND			R18, R31, R19	|	RGB5PCK64	R2, R2
	ADD			R27, R26		|	CMPGT		R16, R17
	SHAD		R19, -4, R3		|	MOV.W?F		R2, (R5)
	ADD			2, R5			|	MOV.W?F		R17, (R6)
	ADD			2, R6			|	CMPGT		R5, R21
	BT			.L0

	MOV.X	(SP,  0), R24
	MOV.X	(SP, 16), R26
	MOV.X	(SP, 32), R28
	MOV.X	(SP, 48), R30
	ADD		64, SP
	RTSU
#endif

#if 1
.ifarch has_xgpr

TKRA_DrawSpan_ModUtx2MortZb:

	MOV.Q	(R4, TKRA_DS_TEXBCN*8), R20

	MOV.X	(R4, TKRA_DS_ZPOS *8), R34
	MOV.X	(R4, TKRA_DS_CPOS *8), R36
	MOV.X	(R4, TKRA_DS_TPOS *8), R38
	MOV.Q	(R4, TKRA_DS_YMASK*8), R41
	LEA.W	(R5, R7), R21
	PMORT.L		R38, R18
	AND			R18, R41, R19
	SHAD		R19, -4, R3

	.L0:	
	ADD			R39, R38		|	MOV.Q		(R20, R3), R2
	SHAD		R34, -16, R17	|	MOV.W		(R6), R16
	PMORT.L		R38, R18		|	BLKUTX2		R2, R19, R2
	ADD			R35, R34		|	PMULU.HW	R2, R36, R2
	AND			R18, R41, R19	|	RGB5PCK64	R2, R2
	ADD			R37, R36		|	CMPGT		R16, R17
	SHAD		R19, -4, R3		|	MOV.W?F		R2, (R5)
	ADD			2, R5			|	MOV.W?F		R17, (R6)
	ADD			2, R6			|	CMPGT		R5, R21
	BT			.L0

//	BREAK

	RTSU

.else

TKRA_DrawSpan_ModUtx2MortZb:
	ADD		-64, SP
	MOV.X	R30, (SP, 48)
	MOV.X	R28, (SP, 32)
	MOV.X	R26, (SP, 16)
	MOV.X	R24, (SP,  0)

	MOV.Q	(R4, TKRA_DS_TEXBCN*8), R20

	MOV.X	(R4, TKRA_DS_ZPOS *8), R24
	MOV.X	(R4, TKRA_DS_CPOS *8), R26
	MOV.X	(R4, TKRA_DS_TPOS *8), R28
//	MOV.Q	(R4, TKRA_DS_XMASK*8), R30
	MOV.Q	(R4, TKRA_DS_YMASK*8), R31
	LEA.W	(R5, R7), R21
	PMORT.L		R28, R18
	AND			R18, R31, R19
//	SHAD		R19, -4, R21
	SHAD		R19, -4, R3

	.L0:	
	ADD			R29, R28		|	MOV.Q		(R20, R3), R2
	SHAD		R24, -16, R17	|	MOV.W		(R6), R16
	PMORT.L		R28, R18		|	BLKUTX2		R2, R19, R2
	ADD			R25, R24		|	PMULU.HW	R2, R26, R2
	AND			R18, R31, R19	|	RGB5PCK64	R2, R2
	ADD			R27, R26		|	CMPGT		R16, R17
	SHAD		R19, -4, R3		|	MOV.W?F		R2, (R5)
	ADD			2, R5			|	MOV.W?F		R17, (R6)
	ADD			2, R6			|	CMPGT		R5, R21
	BT			.L0

	MOV.X	(SP,  0), R24
	MOV.X	(SP, 16), R26
	MOV.X	(SP, 32), R28
	MOV.X	(SP, 48), R30
	ADD		64, SP
	RTSU
.endif

#endif


TKRA_DrawSpan_AtestModUtx2MortZt:
	ADD		-64, SP
	MOV.X	R30, (SP, 48)
	MOV.X	R28, (SP, 32)
	MOV.X	R26, (SP, 16)
	MOV.X	R24, (SP,  0)

	MOV.Q	(R4, TKRA_DS_TEXBCN*8), R20

	MOV.X	(R4, TKRA_DS_ZPOS *8), R24
	MOV.X	(R4, TKRA_DS_CPOS *8), R26
	MOV.X	(R4, TKRA_DS_TPOS *8), R28
	MOV.Q	(R4, TKRA_DS_YMASK*8), R31
	LEA.W	(R5, R7), R21

	.L0:	
									PMORT.L		R28, R2
	ADD			R29, R28		|	AND			R2, R31, R3
	SHAD		R3, -4, R2		|	MOV.W		(R6), R16
	SHAD		R24, -16, R17	|	MOV.Q		(R20, R2), R2
									BLKUTX2		R2, R3, R2
									PMULU.HW	R2, R26, R2
									SHLD.Q		R2, -63, R3
	ADD			R27, R26		|	RGB5PCK64	R2, R2
	ADD			R25, R24		|	TST			1, R3
									CMPGT?F		R16, R17
									MOV.W?F		R2, (R5)
	ADD			2, R5			|	ADD			2, R6

	CMPGT		R5, R21
	BT			.L0

	MOV.X	(SP,  0), R24
	MOV.X	(SP, 16), R26
	MOV.X	(SP, 32), R28
	MOV.X	(SP, 48), R30
	ADD		64, SP
	RTSU

TKRA_DrawSpan_AlphaModUtx2MortZt:
	ADD		-64, SP
	MOV.X	R30, (SP, 48)
	MOV.X	R28, (SP, 32)
	MOV.X	R26, (SP, 16)
	MOV.X	R24, (SP,  0)
	
	MOV.Q	(R4, TKRA_DS_TEXBCN*8), R20

	MOV.X	(R4, TKRA_DS_ZPOS *8), R24
	MOV.X	(R4, TKRA_DS_CPOS *8), R26
	MOV.X	(R4, TKRA_DS_TPOS *8), R28

//	MOV.Q	(R4, TKRA_DS_ZPOS *8), R24
//	MOV.Q	(R4, TKRA_DS_ZSTEP*8), R25
//	MOV.Q	(R4, TKRA_DS_CPOS *8), R26
//	MOV.Q	(R4, TKRA_DS_CSTEP*8), R27
//	MOV.Q	(R4, TKRA_DS_TPOS *8), R28
//	MOV.Q	(R4, TKRA_DS_TSTEP*8), R29
//	MOV.Q	(R4, TKRA_DS_XMASK*8), R30
	MOV.Q	(R4, TKRA_DS_YMASK*8), R31
	LEA.W	(R5, R7), R21

	.L1:
	PMORT.L		R28, R18		|	MOVU.W		(R5), R19
	ADD			R29, R28		|	AND			R18, R31, R3
									SHAD		R3, -4, R18
									MOV.Q		(R20, R18), R18
									BLKUTX2		R18, R3, R18
	RGB5UPCK64	R19, R19		|	PMULU.HW	R18, R26, R18
									PSHUF.W		R18, 0xFF, R22
									NOT			R22, R23
	PMULU.HW	R18, R22, R2	|	PMULU.HW	R19, R23, R3
	SHAD.Q		R24, -16, R17	|	PADD.W		R2, R3, R18
	ADD			R27, R26		|	RGB5PCK64	R18, R18
	ADD			R25, R24		|	MOV.W		(R6), R16
									CMPGT		R16, R17
									MOV.W?F		R18, (R5)
	ADD			2, R5			|	ADD			2, R6
	CMPGT		R5, R21
	BT			.L1

	.L2:

	MOV.X	(SP,  0), R24
	MOV.X	(SP, 16), R26
	MOV.X	(SP, 32), R28
	MOV.X	(SP, 48), R30
	ADD		64, SP
	RTSU

TKRA_DrawSpan_AlphaModUtx2MortZb:
	ADD		-64, SP
	MOV.X	R30, (SP, 48)
	MOV.X	R28, (SP, 32)
	MOV.X	R26, (SP, 16)
	MOV.X	R24, (SP,  0)
	
	MOV.Q	(R4, TKRA_DS_TEXBCN*8), R20
	MOV.X	(R4, TKRA_DS_ZPOS *8), R24
	MOV.X	(R4, TKRA_DS_CPOS *8), R26
	MOV.X	(R4, TKRA_DS_TPOS *8), R28
	MOV.Q	(R4, TKRA_DS_YMASK*8), R31
	LEA.W	(R5, R7), R21

	.L1:
//	CMPQGT		R5, R21
//	BF			.L2

	PMORT.L		R28, R18		|	MOVU.W		(R5), R19
	ADD			R29, R28		|	AND			R18, R31, R3
									SHAD		R3, -4, R18
									MOV.Q		(R20, R18), R18
									BLKUTX2		R18, R3, R18
									RGB5UPCK64	R19, R19
									PMULU.HW	R18, R26, R18
									PSHUF.W		R18, 0xFF, R22
									NOT			R22, R23
	PMULU.HW	R18, R22, R2	|	PMULU.HW	R19, R23, R3
	SHAD.Q		R24, -16, R17	|	PADD.W		R2, R3, R18
	ADD			R27, R26		|	RGB5PCK64	R18, R18
	ADD			R25, R24		|	MOV.W		(R6), R16
									CMPGT		R16, R17
									MOV.W?F		R18, (R5)
									MOV.W?F		R17, (R6)
	ADD			2, R5			|	ADD			2, R6
	CMPGT		R5, R21
	BT			.L1

	.L2:

	MOV.X	(SP,  0), R24
	MOV.X	(SP, 16), R26
	MOV.X	(SP, 32), R28
	MOV.X	(SP, 48), R30
	ADD		64, SP
	RTSU

TKRA_DrawSpan_ModBlUtx2MortZt:
	ADD		-128, SP
	MOV.X	R30, (SP, 112)
	MOV.X	R28, (SP,  96)
	MOV.X	R26, (SP,  80)
	MOV.X	R24, (SP,  64)
	MOV.Q	R14, (SP,  48)
	MOV.X	R12, (SP,  32)
	MOV.X	R10, (SP,  16)
	MOV.X	R8 , (SP,   0)

	MOV.Q	(R4, TKRA_DS_TEXBCN*8), R20
	MOV.X	(R4, TKRA_DS_ZPOS *8), R24
	MOV.X	(R4, TKRA_DS_CPOS *8), R26
	MOV.X	(R4, TKRA_DS_TPOS *8), R28
	MOV.Q	(R4, TKRA_DS_YMASK*8), R31
	LEA.W	(R5, R7), R21

	.L1:
	MOV			0x00000000FFFF, R8
	MOV			0xFFFF00000000, R9
	ADD			R28, R8, R17	|	PMORT.L		R28, R18
	AND			R18, R31, R2	|	PMORT.L		R17, R19
	AND			R19, R31, R3	|	ADD			R28, R9, R16
	SHAD		R2, -4, R18		|	SHAD		R3, -4, R19
	ADD			R16, R8, R17	|	MOV.Q		(R20, R18), R18
	PMORT.L		R16, R22		|	MOV.Q		(R20, R19), R19
	AND			R22, R31, R22	|	BLKUTX2		R18, R2, R8
	PMORT.L		R17, R23		|	BLKUTX2		R19, R3, R9
	AND			R23, R31, R23	|	MOV			R28, R16
	SHAD		R22, -4, R10	|	SHAD		R23, -4, R11
									MOV.Q		(R20, R10), R10
									MOV.Q		(R20, R11), R11
									BLKUTX2		R10, R22, R10
									BLKUTX2		R11, R23, R11
	ADD			R29, R28		|	PSHUF.W		R16, 0, R22
	NOT			R22, R23		|	PSHUF.W		R16, 0xAA, R18
	NOT			R18, R19		|
	PMULU.HW	R8, R23, R12	|	PMULU.HW	R9, R22, R13
	PMULU.HW	R10, R23, R14	|	PMULU.HW	R11, R22, R30
	PADD.W		R12, R13, R8	|	PADD.W		R14, R30, R9
	PMULU.HW	R8, R19, R12	|	PMULU.HW	R9, R18, R13
									PADD.W		R12, R13, R18
									PMULU.HW	R18, R26, R18
	ADD			R27, R26		|	RGB5PCK64	R18, R18
	SHAD.Q		R24, -16, R17	|	MOV.W		(R6), R16
	ADD			R25, R24		|	CMPGT		R16, R17
									MOV.W?F		R18, (R5)
	ADD			2, R5			|	ADD			2, R6

	CMPGT		R5, R21
	BT			.L1

	.L2:
	MOV.X	(SP,   0), R8
	MOV.X	(SP,  16), R10
	MOV.X	(SP,  32), R12
	MOV.Q	(SP,  48), R14
	MOV.X	(SP,  64), R24
	MOV.X	(SP,  80), R26
	MOV.X	(SP,  96), R28
	MOV.X	(SP, 112), R30
	ADD		128, SP
	RTSU

TKRA_DrawSpan_AlphaModBlUtx2MortZt:
	ADD		-128, SP
	MOV.X	R30, (SP, 112)
	MOV.X	R28, (SP,  96)
	MOV.X	R26, (SP,  80)
	MOV.X	R24, (SP,  64)
	MOV.Q	R14, (SP,  48)
	MOV.X	R12, (SP,  32)
	MOV.X	R10, (SP,  16)
	MOV.X	R8 , (SP,   0)

	MOV.Q	(R4, TKRA_DS_TEXBCN*8), R20
	MOV.X	(R4, TKRA_DS_ZPOS *8), R24
	MOV.X	(R4, TKRA_DS_CPOS *8), R26
	MOV.X	(R4, TKRA_DS_TPOS *8), R28
	MOV.Q	(R4, TKRA_DS_YMASK*8), R31
	LEA.W	(R5, R7), R21

	.L1:
	MOV			0x00000000FFFF, R8
	MOV			0xFFFF00000000, R9
	ADD			R28, R8, R17	|	PMORT.L		R28, R18
	AND			R18, R31, R2	|	PMORT.L		R17, R19
	AND			R19, R31, R3	|	ADD			R28, R9, R16

	SHAD		R2, -4, R18		|	SHAD		R3, -4, R19
	ADD			R16, R8, R17	|	MOV.Q		(R20, R18), R18
	PMORT.L		R16, R22		|	MOV.Q		(R20, R19), R19
	AND			R22, R31, R22	|	BLKUTX2		R18, R2, R8
	PMORT.L		R17, R23		|	BLKUTX2		R19, R3, R9

	AND			R23, R31, R23	|	MOV			R28, R16		
	SHAD		R22, -4, R10	|	SHAD		R23, -4, R11
									MOV.Q		(R20, R10), R10
									MOV.Q		(R20, R11), R11
									BLKUTX2		R10, R22, R10
									BLKUTX2		R11, R23, R11

	ADD			R29, R28		|	PSHUF.W		R16, 0, R22
	NOT			R22, R23		|	PSHUF.W		R16, 0xAA, R18
	NOT			R18, R19		|
	PMULU.HW	R8, R23, R12	|	PMULU.HW	R9, R22, R13
	PMULU.HW	R10, R23, R14	|	PMULU.HW	R11, R22, R30
	PADD.W		R12, R13, R8	|	PADD.W		R14, R30, R9
	PMULU.HW	R8, R19, R12	|	PMULU.HW	R9, R18, R13
	PADD.W		R12, R13, R18	|	MOV.W		(R5), R19
									PMULU.HW	R18, R26, R18
									PSHUF.W		R18, 0xFF, R22
	RGB5UPCK64	R19, R19		|	NOT			R22, R23
	PMULU.HW	R18, R22, R12	|	PMULU.HW	R19, R23, R13
	ADD			R27, R26		|	PADD.W		R12, R13, R18
									RGB5PCK64	R18, R18
	SHAD.Q		R24, -16, R17	|	MOV.W		(R6), R16
	ADD			R25, R24		|	CMPGT		R16, R17
									MOV.W?F		R18, (R5)
	ADD			2, R5			|	ADD			2, R6
	CMPGT		R5, R21
	BT			.L1

	.L2:
	MOV.X	(SP,   0), R8
	MOV.X	(SP,  16), R10
	MOV.X	(SP,  32), R12
	MOV.Q	(SP,  48), R14
	MOV.X	(SP,  64), R24
	MOV.X	(SP,  80), R26
	MOV.X	(SP,  96), R28
	MOV.X	(SP, 112), R30
	ADD		128, SP
	RTSU

TKRA_DrawSpan_AtestModBlUtx2MortZt:
	ADD		-128, SP
	MOV.X	R30, (SP, 112)
	MOV.X	R28, (SP,  96)
	MOV.X	R26, (SP,  80)
	MOV.X	R24, (SP,  64)
	MOV.Q	R14, (SP,  48)
	MOV.X	R12, (SP,  32)
	MOV.X	R10, (SP,  16)
	MOV.X	R8 , (SP,   0)

	MOV.Q	(R4, TKRA_DS_TEXBCN*8), R20
	MOV.X	(R4, TKRA_DS_ZPOS *8), R24
	MOV.X	(R4, TKRA_DS_CPOS *8), R26
	MOV.X	(R4, TKRA_DS_TPOS *8), R28
	MOV.Q	(R4, TKRA_DS_YMASK*8), R31
	LEA.W	(R5, R7), R21

	.L1:
	MOV			0x00000000FFFF, R8
	MOV			0xFFFF00000000, R9
	ADD			R28, R8, R17	|	PMORT.L		R28, R18
	AND			R18, R31, R2	|	PMORT.L		R17, R19
	AND			R19, R31, R3	|	ADD			R28, R9, R16
	SHAD		R2, -4, R18		|	SHAD		R3, -4, R19
	ADD			R16, R8, R17	|	MOV.Q		(R20, R18), R18
	PMORT.L		R16, R22		|	MOV.Q		(R20, R19), R19
	AND			R22, R31, R22	|	BLKUTX2		R18, R2, R8
	PMORT.L		R17, R23		|	BLKUTX2		R19, R3, R9
	AND			R23, R31, R23	|	MOV			R28, R16
	SHAD		R22, -4, R10	|	SHAD		R23, -4, R11
									MOV.Q		(R20, R10), R10
									MOV.Q		(R20, R11), R11
									BLKUTX2		R10, R22, R10
									BLKUTX2		R11, R23, R11
	ADD			R29, R28		|	PSHUF.W		R16, 0, R22
	NOT			R22, R23		|	PSHUF.W		R16, 0xAA, R18
	NOT			R18, R19		|
	PMULU.HW	R8, R23, R12	|	PMULU.HW	R9, R22, R13
	PMULU.HW	R10, R23, R14	|	PMULU.HW	R11, R22, R30
	PADD.W		R12, R13, R8	|	PADD.W		R14, R30, R9
	PMULU.HW	R8, R19, R12	|	PMULU.HW	R9, R18, R13
//									PADD.W		R12, R13, R18
//									PMULU.HW	R18, R26, R18
//									SHLD.Q		R18, -63, R3

									PADD.W		R12, R13, R19
	SHLD.Q		R19, -63, R3	|	PMULU.HW	R19, R26, R18

	ADD			R27, R26		|	RGB5PCK64	R18, R18
	SHAD.Q		R24, -16, R17	|	MOV.W		(R6), R16
	ADD			R25, R24		|	TST			1, R3
									CMPGT?F		R16, R17
									MOV.W?F		R18, (R5)
	ADD			2, R5			|	ADD			2, R6

	CMPGT		R5, R21
	BT			.L1

	.L2:
	MOV.X	(SP,   0), R8
	MOV.X	(SP,  16), R10
	MOV.X	(SP,  32), R12
	MOV.Q	(SP,  48), R14
	MOV.X	(SP,  64), R24
	MOV.X	(SP,  80), R26
	MOV.X	(SP,  96), R28
	MOV.X	(SP, 112), R30
	ADD		128, SP
	RTSU

#if 1
.ifarch has_xgpr

TKRA_DrawSpan_ModBlUtx2MortZb:
	MOV.Q	(R4, TKRA_DS_TEXBCN*8), R20
	MOV.X	(R4, TKRA_DS_ZPOS *8), R34
	MOV.X	(R4, TKRA_DS_CPOS *8), R36
	MOV.X	(R4, TKRA_DS_TPOS *8), R38
	MOV.Q	(R4, TKRA_DS_YMASK*8), R33
	LEA.W	(R5, R7), R21

	.L1:
	MOV			0x00000000FFFF, R48
	MOV			0xFFFF00000000, R49

#ifdef TKRA_CHEAP_BILIN
	ADD			R38, R48, R17	|	PMORT.L		R38, R18
	AND			R18, R33, R2	|	PMORT.L		R17, R19
	AND			R19, R33, R3	|	ADD			R38, R49, R16
	SHAD		R2, -4, R18		|	SHAD		R3, -4, R19
	PMORT.L		R16, R22		|	MOV.Q		(R20, R18), R18
	AND			R22, R33, R22	|	MOV.Q		(R20, R19), R19
	MOV			0x7FFF00007FFF, R51
	SHLD.Q		R38, -1, R16	|	BLKUTX2		R18, R2, R48
	AND			R51, R16		|	BLKUTX2		R19, R3, R49
	PSHUF.W		R16, 0, R17		|	SHAD		R22, -4, R50
	PSHUF.W		R16, 0xAA, R18	|	MOV.Q		(R20, R50), R50
	PADD.W		R17, R18, R19	|	PMULU.HW	R49, R17, R53
	ADD			R39, R38		|	NOT			R19, R23
	PMULU.HW	R48, R23, R52	|	BLKUTX2		R50, R22, R50
	PMULU.HW	R50, R18, R54	|	PADD.W		R52, R53, R48	
									PADD.W		R48, R54, R18
									PMULU.HW	R18, R36, R18
	PADD.W		R36, R37, R36	|	RGB5PCK64	R18, R18
#else
	ADD			R38, R48, R17	|	PMORT.L		R38, R18
	AND			R18, R33, R2	|	PMORT.L		R17, R19
	AND			R19, R33, R3	|	ADD			R38, R49, R16
	SHAD		R2, -4, R18		|	SHAD		R3, -4, R19
	ADD			R16, R48, R17	|	MOV.Q		(R20, R18), R18
	PMORT.L		R16, R22		|	MOV.Q		(R20, R19), R19
	AND			R22, R33, R22	|	BLKUTX2		R18, R2, R48
	PMORT.L		R17, R23		|	BLKUTX2		R19, R3, R49
	AND			R23, R33, R23	|	MOV			R38, R16
	SHAD		R22, -4, R50	|	SHAD		R23, -4, R51
									MOV.Q		(R20, R50), R50
									MOV.Q		(R20, R51), R51
									BLKUTX2		R50, R22, R50
									BLKUTX2		R51, R23, R51
	ADD			R39, R38		|	PSHUF.W		R16, 0, R22
	NOT			R22, R23		|	PSHUF.W		R16, 0xAA, R18
	NOT			R18, R19		|
	PMULU.HW	R48, R23, R52	|	PMULU.HW	R49, R22, R53
	PMULU.HW	R50, R23, R54	|	PMULU.HW	R51, R22, R32
	PADD.W		R52, R53, R48	|	PADD.W		R54, R32, R49
	PMULU.HW	R48, R19, R52	|	PMULU.HW	R49, R18, R53
									PADD.W		R52, R53, R18
									PMULU.HW	R18, R36, R18
	ADD			R37, R36		|	RGB5PCK64	R18, R18
#endif
	SHAD.Q		R34, -16, R17	|	MOV.W		(R6), R16
	ADD			R35, R34		|	CMPGT		R16, R17
									MOV.W?F		R18, (R5)
									MOV.W?F		R17, (R6)
	ADD			2, R5			|	ADD			2, R6

	CMPGT		R5, R21
	BT			.L1

	.L2:
	RTSU

.else

TKRA_DrawSpan_ModBlUtx2MortZb:
	ADD		-128, SP
	MOV.X	R30, (SP, 112)
	MOV.X	R28, (SP,  96)
	MOV.X	R26, (SP,  80)
	MOV.X	R24, (SP,  64)
	MOV.Q	R14, (SP,  48)
	MOV.X	R12, (SP,  32)
	MOV.X	R10, (SP,  16)
	MOV.X	R8 , (SP,   0)

	MOV.Q	(R4, TKRA_DS_TEXBCN*8), R20
	MOV.X	(R4, TKRA_DS_ZPOS *8), R24
	MOV.X	(R4, TKRA_DS_CPOS *8), R26
	MOV.X	(R4, TKRA_DS_TPOS *8), R28
	MOV.Q	(R4, TKRA_DS_YMASK*8), R31
	LEA.W	(R5, R7), R21

	.L1:
	MOV			0x00000000FFFF, R8
	MOV			0xFFFF00000000, R9

#ifdef TKRA_CHEAP_BILIN
	ADD			R28, R8, R17	|	PMORT.L		R28, R18
	AND			R18, R31, R2	|	PMORT.L		R17, R19
	AND			R19, R31, R3	|	ADD			R28, R9, R16
	SHAD		R2, -4, R18		|	SHAD		R3, -4, R19
	PMORT.L		R16, R22		|	MOV.Q		(R20, R18), R18
	AND			R22, R31, R22	|	MOV.Q		(R20, R19), R19
	MOV			0x7FFF00007FFF, R11
	SHLD.Q		R28, -1, R16	|	BLKUTX2		R18, R2, R8
	AND			R11, R16		|	BLKUTX2		R19, R3, R9
	PSHUF.W		R16, 0, R17		|	SHAD		R22, -4, R10
	PSHUF.W		R16, 0xAA, R18	|	MOV.Q		(R20, R10), R10
	PADD.W		R17, R18, R19	|	PMULU.HW	R9, R17, R13
	ADD			R29, R28		|	NOT			R19, R23
	PMULU.HW	R8, R23, R12	|	BLKUTX2		R10, R22, R10
	PMULU.HW	R10, R18, R14	|	PADD.W		R12, R13, R8	
									PADD.W		R8, R14, R18
									PMULU.HW	R18, R26, R18
	PADD.W		R26, R27, R26	|	RGB5PCK64	R18, R18
#else
	ADD			R28, R8, R17	|	PMORT.L		R28, R18
	AND			R18, R31, R2	|	PMORT.L		R17, R19
	AND			R19, R31, R3	|	ADD			R28, R9, R16
	SHAD		R2, -4, R18		|	SHAD		R3, -4, R19
	ADD			R16, R8, R17	|	MOV.Q		(R20, R18), R18
	PMORT.L		R16, R22		|	MOV.Q		(R20, R19), R19
	AND			R22, R31, R22	|	BLKUTX2		R18, R2, R8
	PMORT.L		R17, R23		|	BLKUTX2		R19, R3, R9
	AND			R23, R31, R23	|	MOV			R28, R16
	SHAD		R22, -4, R10	|	SHAD		R23, -4, R11
									MOV.Q		(R20, R10), R10
									MOV.Q		(R20, R11), R11
									BLKUTX2		R10, R22, R10
									BLKUTX2		R11, R23, R11
	ADD			R29, R28		|	PSHUF.W		R16, 0, R22
	NOT			R22, R23		|	PSHUF.W		R16, 0xAA, R18
	NOT			R18, R19		|
	PMULU.HW	R8, R23, R12	|	PMULU.HW	R9, R22, R13
	PMULU.HW	R10, R23, R14	|	PMULU.HW	R11, R22, R30
	PADD.W		R12, R13, R8	|	PADD.W		R14, R30, R9
	PMULU.HW	R8, R19, R12	|	PMULU.HW	R9, R18, R13
									PADD.W		R12, R13, R18
									PMULU.HW	R18, R26, R18
	ADD			R27, R26		|	RGB5PCK64	R18, R18
#endif
	SHAD.Q		R24, -16, R17	|	MOV.W		(R6), R16
	ADD			R25, R24		|	CMPGT		R16, R17
									MOV.W?F		R18, (R5)
									MOV.W?F		R17, (R6)
	ADD			2, R5			|	ADD			2, R6

	CMPGT		R5, R21
	BT			.L1

	.L2:
	MOV.X	(SP,   0), R8
	MOV.X	(SP,  16), R10
	MOV.X	(SP,  32), R12
	MOV.Q	(SP,  48), R14
	MOV.X	(SP,  64), R24
	MOV.X	(SP,  80), R26
	MOV.X	(SP,  96), R28
	MOV.X	(SP, 112), R30
	ADD		128, SP
	RTSU

.endif

#endif

#if 1
TKRA_DrawSpan_AlphaModBlUtx2MortZb:
	ADD		-128, SP
	MOV.X	R30, (SP, 112)
	MOV.X	R28, (SP,  96)
	MOV.X	R26, (SP,  80)
	MOV.X	R24, (SP,  64)
	MOV.Q	R14, (SP,  48)
	MOV.X	R12, (SP,  32)
	MOV.X	R10, (SP,  16)
	MOV.X	R8 , (SP,   0)

	MOV.Q	(R4, TKRA_DS_TEXBCN*8), R20
	MOV.X	(R4, TKRA_DS_ZPOS *8), R24
	MOV.X	(R4, TKRA_DS_CPOS *8), R26
	MOV.X	(R4, TKRA_DS_TPOS *8), R28
	MOV.Q	(R4, TKRA_DS_YMASK*8), R31
	LEA.W	(R5, R7), R21

	.L1:
	MOV			0x00000000FFFF, R8
	MOV			0xFFFF00000000, R9
	ADD			R28, R8, R17	|	PMORT.L		R28, R18
	AND			R18, R31, R2	|	PMORT.L		R17, R19
	AND			R19, R31, R3	|	ADD			R28, R9, R16

	SHAD		R2, -4, R18		|	SHAD		R3, -4, R19
	ADD			R16, R8, R17	|	MOV.Q		(R20, R18), R18
	PMORT.L		R16, R22		|	MOV.Q		(R20, R19), R19
	AND			R22, R31, R22	|	BLKUTX2		R18, R2, R8
	PMORT.L		R17, R23		|	BLKUTX2		R19, R3, R9

	AND			R23, R31, R23	|	MOV			R28, R16		
	SHAD		R22, -4, R10	|	SHAD		R23, -4, R11
									MOV.Q		(R20, R10), R10
									MOV.Q		(R20, R11), R11
									BLKUTX2		R10, R22, R10
									BLKUTX2		R11, R23, R11

	ADD			R29, R28		|	PSHUF.W		R16, 0, R22
	NOT			R22, R23		|	PSHUF.W		R16, 0xAA, R18
	NOT			R18, R19		|
	PMULU.HW	R8, R23, R12	|	PMULU.HW	R9, R22, R13
	PMULU.HW	R10, R23, R14	|	PMULU.HW	R11, R22, R30
	PADD.W		R12, R13, R8	|	PADD.W		R14, R30, R9
	PMULU.HW	R8, R19, R12	|	PMULU.HW	R9, R18, R13
	PADD.W		R12, R13, R18	|	MOV.W		(R5), R19
									PMULU.HW	R18, R26, R18
									PSHUF.W		R18, 0xFF, R22
	RGB5UPCK64	R19, R19		|	NOT			R22, R23
	PMULU.HW	R18, R22, R12	|	PMULU.HW	R19, R23, R13
	ADD			R27, R26		|	PADD.W		R12, R13, R18
									RGB5PCK64	R18, R18
	SHAD.Q		R24, -16, R17	|	MOV.W		(R6), R16
	ADD			R25, R24		|	CMPGT		R16, R17
									MOV.W?F		R18, (R5)
									MOV.W?F		R17, (R6)
	ADD			2, R5			|	ADD			2, R6
	CMPGT		R5, R21
	BT			.L1

	.L2:
	MOV.X	(SP,   0), R8
	MOV.X	(SP,  16), R10
	MOV.X	(SP,  32), R12
	MOV.Q	(SP,  48), R14
	MOV.X	(SP,  64), R24
	MOV.X	(SP,  80), R26
	MOV.X	(SP,  96), R28
	MOV.X	(SP, 112), R30
	ADD		128, SP
	RTSU
#endif

TKRA_DrawSpan_ZbNul:
	RTS

TKRA_DrawSpan_Zbuf:
	LEA.W	(R4, R5), R21
	.L0:	
	SHAD		R6, -16, R17	| MOV.W		(R4), R16
	ADD			R7, R6			| CMPGT		R16, R17
	MOV.W?F		R17, (R4)
	ADD			2, R4
	CMPGT		R4, R21
	BT			.L0
	RTSU

#if 0
TKRA_DrawSpan_ModBlUtx2MortZb:
	ADD		-64, SP
	MOV		LR, R16
	MOV.X	R4, (SP,  0)
	MOV.X	R6, (SP, 16)
	MOV.Q	R16, (SP, 32)
	
	MOV		R4, R20
	MOV		R6, R4
	MOV		R7, R5
	MOV.X	(R20, TKRA_DS_ZPOS *8), R6
	BSR		TKRA_DrawSpan_Zbuf
	
	MOV.X	(SP,  0), R4
	MOV.X	(SP, 16), R6
	BSR TKRA_DrawSpan_ModBlUtx2MortZt

	MOV.Q	(SP, 32), R16
	ADD		64, SP
	JMP		R16

TKRA_DrawSpan_AlphaModBlUtx2MortZb:
	ADD		-64, SP
	MOV		LR, R16
	MOV.X	R4, (SP,  0)
	MOV.X	R6, (SP, 16)
	MOV.Q	R16, (SP, 32)
	
	MOV		R4, R20
	MOV		R6, R4
	MOV		R7, R5
	MOV.X	(R20, TKRA_DS_ZPOS *8), R6
	BSR		TKRA_DrawSpan_Zbuf
	
	MOV.X	(SP,  0), R4
	MOV.X	(SP, 16), R6
	BSR TKRA_DrawSpan_AlphaModBlUtx2MortZt

	MOV.Q	(SP, 32), R16
	ADD		64, SP
	JMP		R16
#endif


TKRA_DrawSpan_AtestModTexMortZt:
	ADD		-64, SP
	MOV.X	R30, (SP, 48)
	MOV.X	R28, (SP, 32)
	MOV.X	R26, (SP, 16)
	MOV.X	R24, (SP,  0)

	MOV.Q	(R4, TKRA_DS_TEXIMG*8), R20
	MOV.X	(R4, TKRA_DS_ZPOS *8), R24
	MOV.X	(R4, TKRA_DS_CPOS *8), R26
	MOV.X	(R4, TKRA_DS_TPOS *8), R28
	MOV.Q	(R4, TKRA_DS_YMASK*8), R31
	LEA.W	(R5, R7), R21

	.L0:	
	PMORT.L		R28, R2
	ADD			R29, R28		|	AND			R2, R31, R3
	MOV.W		(R20, R3), R2
	RGB5UPCK64	R2, R2
	PMULU.HW	R2, R26, R2
	ADD			R27, R26		| RGB5PCK64		R2, R3
	SHAD		R24, -16, R17	| MOV.W		(R6), R16
	ADD			R25, R24

	MOV			0x8000000000000000, R1
	TSTQ		R1, R2
	CMPGT?F		R16, R17
	MOV.W?F		R3, (R5)

	ADD			2, R5			| ADD			2, R6
	CMPGT		R5, R21
	BT			.L0

	MOV.X	(SP,  0), R24
	MOV.X	(SP, 16), R26
	MOV.X	(SP, 32), R28
	MOV.X	(SP, 48), R30
	ADD		64, SP
	RTSU

TKRA_DrawSpan_AtestModTexMortZb:
	ADD		-64, SP
	MOV.X	R30, (SP, 48)
	MOV.X	R28, (SP, 32)
	MOV.X	R26, (SP, 16)
	MOV.X	R24, (SP,  0)

	MOV.Q	(R4, TKRA_DS_TEXIMG*8), R20
	MOV.X	(R4, TKRA_DS_ZPOS *8), R24
	MOV.X	(R4, TKRA_DS_CPOS *8), R26
	MOV.X	(R4, TKRA_DS_TPOS *8), R28
	MOV.Q	(R4, TKRA_DS_YMASK*8), R31
	LEA.W	(R5, R7), R21

	.L0:	
	PMORT.L		R28, R2
	ADD			R29, R28		|	AND			R2, R31, R3
	MOV.W		(R20, R3), R2
	RGB5UPCK64	R2, R2
	PMULU.HW	R2, R26, R2
	ADD			R27, R26		| RGB5PCK64		R2, R3
	SHAD		R24, -16, R17	| MOV.W		(R6), R16
	ADD			R25, R24

	MOV			0x8000000000000000, R1
	TSTQ		R1, R2
	CMPGT?F		R16, R17
	MOV.W?F		R3, (R5)
	MOV.W?F		R17, (R6)

	ADD			2, R5			| ADD			2, R6
	CMPGT		R5, R21
	BT			.L0

	MOV.X	(SP,  0), R24
	MOV.X	(SP, 16), R26
	MOV.X	(SP, 32), R28
	MOV.X	(SP, 48), R30
	ADD		64, SP
	RTSU

TKRA_DrawSpan_AtestModUtx2MortZb:
	ADD		-64, SP
	MOV.X	R30, (SP, 48)
	MOV.X	R28, (SP, 32)
	MOV.X	R26, (SP, 16)
	MOV.X	R24, (SP,  0)

	MOV.Q	(R4, TKRA_DS_TEXBCN*8), R20

	MOV.X	(R4, TKRA_DS_ZPOS *8), R24
	MOV.X	(R4, TKRA_DS_CPOS *8), R26
	MOV.X	(R4, TKRA_DS_TPOS *8), R28
	MOV.Q	(R4, TKRA_DS_YMASK*8), R31
	LEA.W	(R5, R7), R21

	.L0:	
									PMORT.L		R28, R2
	ADD			R29, R28		|	AND			R2, R31, R3
	SHAD		R3, -4, R2		|	MOV.W		(R6), R16
	SHAD		R24, -16, R17	|	MOV.Q		(R20, R2), R2
									BLKUTX2		R2, R3, R2
									PMULU.HW	R2, R26, R2
	ADD			R27, R26		|	RGB5PCK64	R2, R3
	ADD			R25, R24
									MOV			0x8000000000000000, R1
									TSTQ		R1, R2
									CMPGT?F		R16, R17
									MOV.W?F		R3, (R5)
									MOV.W?F		R17, (R6)
	ADD			2, R5			|	ADD			2, R6

	CMPGT		R5, R21
	BT			.L0

	MOV.X	(SP,  0), R24
	MOV.X	(SP, 16), R26
	MOV.X	(SP, 32), R28
	MOV.X	(SP, 48), R30
	ADD		64, SP
	RTSU

TKRA_DrawSpan_AtestModBlTexMortZt:
	ADD		-128, SP
	MOV.X	R30, (SP, 112)
	MOV.X	R28, (SP,  96)
	MOV.X	R26, (SP,  80)
	MOV.X	R24, (SP,  64)
	MOV.Q	R14, (SP,  48)
	MOV.X	R12, (SP,  32)
	MOV.X	R10, (SP,  16)
	MOV.X	R8 , (SP,   0)

	MOV.Q	(R4, TKRA_DS_TEXIMG*8), R20
	MOV.X	(R4, TKRA_DS_ZPOS *8), R24
	MOV.X	(R4, TKRA_DS_CPOS *8), R26
	MOV.X	(R4, TKRA_DS_TPOS *8), R28
	MOV.Q	(R4, TKRA_DS_YMASK*8), R31
	LEA.W	(R5, R7), R21

	.L1:
	MOV			0x00000000FFFF, R8
	MOV			0xFFFF00000000, R9
	ADD			R28, R8, R17	|	PMORT.L		R28, R18
	AND			R18, R31, R2	|	PMORT.L		R17, R19
	AND			R19, R31, R3	|	MOV.W		(R20, R2), R18
	ADD			R28, R9, R16	|	MOV.W		(R20, R3), R19
	ADD			R16, R8, R17	|	PMORT.L		R16, R22
	AND			R22, R31, R22	|	PMORT.L		R17, R23
	AND			R23, R31, R23	|	MOV.W		(R20, R22), R10
	MOV			R28, R16		|	MOV.W		(R20, R23), R11
	ADD			R29, R28		|	PSHUF.W		R16, 0, R22
	RGB5UPCK64	R18, R8			|	RGB5UPCK64	R19, R9
	NOT			R22, R23		|	PSHUF.W		R16, 0xAA, R18
	NOT			R18, R19		|
	RGB5UPCK64	R10, R10		|	RGB5UPCK64	R11, R11
	PMULU.HW	R8, R23, R12	|	PMULU.HW	R9, R22, R13
	PMULU.HW	R10, R23, R14	|	PMULU.HW	R11, R22, R30
	PADD.W		R12, R13, R8	|	PADD.W		R14, R30, R9
	PMULU.HW	R8, R19, R12	|	PMULU.HW	R9, R18, R13
									PADD.W		R12, R13, R18
									PMULU.HW	R18, R26, R18
	ADD			R27, R26		|	RGB5PCK64	R18, R19
	SHAD.Q		R24, -16, R17	|	MOV.W		(R6), R16
	ADD			R25, R24
									MOV			0x8000000000000000, R1
									TSTQ		R1, R18
									CMPGT?F		R16, R17
									MOV.W?F		R19, (R5)
	ADD			2, R5			|	ADD			2, R6

	CMPGT		R5, R21
	BT			.L1

	.L2:

	MOV.X	(SP,   0), R8
	MOV.X	(SP,  16), R10
	MOV.X	(SP,  32), R12
	MOV.Q	(SP,  48), R14
	MOV.X	(SP,  64), R24
	MOV.X	(SP,  80), R26
	MOV.X	(SP,  96), R28
	MOV.X	(SP, 112), R30
	ADD		128, SP
	RTSU

TKRA_DrawSpan_AtestModBlUtx2MortZb:
	ADD		-128, SP
	MOV.X	R30, (SP, 112)
	MOV.X	R28, (SP,  96)
	MOV.X	R26, (SP,  80)
	MOV.X	R24, (SP,  64)
	MOV.Q	R14, (SP,  48)
	MOV.X	R12, (SP,  32)
	MOV.X	R10, (SP,  16)
	MOV.X	R8 , (SP,   0)

	MOV.Q	(R4, TKRA_DS_TEXBCN*8), R20
	MOV.X	(R4, TKRA_DS_ZPOS *8), R24
	MOV.X	(R4, TKRA_DS_CPOS *8), R26
	MOV.X	(R4, TKRA_DS_TPOS *8), R28
	MOV.Q	(R4, TKRA_DS_YMASK*8), R31
	LEA.W	(R5, R7), R21

	.L1:
	MOV			0x00000000FFFF, R8
	MOV			0xFFFF00000000, R9
	ADD			R28, R8, R17	|	PMORT.L		R28, R18
	AND			R18, R31, R2	|	PMORT.L		R17, R19
	AND			R19, R31, R3	|	ADD			R28, R9, R16
	SHAD		R2, -4, R18		|	SHAD		R3, -4, R19
	ADD			R16, R8, R17	|	MOV.Q		(R20, R18), R18
	PMORT.L		R16, R22		|	MOV.Q		(R20, R19), R19
	AND			R22, R31, R22	|	BLKUTX2		R18, R2, R8
	PMORT.L		R17, R23		|	BLKUTX2		R19, R3, R9
	AND			R23, R31, R23	|	MOV			R28, R16
	SHAD		R22, -4, R10	|	SHAD		R23, -4, R11
									MOV.Q		(R20, R10), R10
									MOV.Q		(R20, R11), R11
									BLKUTX2		R10, R22, R10
									BLKUTX2		R11, R23, R11
	ADD			R29, R28		|	PSHUF.W		R16, 0, R22
	NOT			R22, R23		|	PSHUF.W		R16, 0xAA, R18
	NOT			R18, R19		|
	PMULU.HW	R8, R23, R12	|	PMULU.HW	R9, R22, R13
	PMULU.HW	R10, R23, R14	|	PMULU.HW	R11, R22, R30
	PADD.W		R12, R13, R8	|	PADD.W		R14, R30, R9
	PMULU.HW	R8, R19, R12	|	PMULU.HW	R9, R18, R13
									PADD.W		R12, R13, R18
									PMULU.HW	R18, R26, R18
	ADD			R27, R26		|	RGB5PCK64	R18, R19
	SHAD.Q		R24, -16, R17	|	MOV.W		(R6), R16
	ADD			R25, R24		
									MOV			0x8000000000000000, R1
									TSTQ		R1, R18
									CMPGT?F		R16, R17
									MOV.W?F		R19, (R5)
									MOV.W?F		R17, (R6)
	ADD			2, R5			|	ADD			2, R6

	CMPGT		R5, R21
	BT			.L1

	.L2:
	MOV.X	(SP,   0), R8
	MOV.X	(SP,  16), R10
	MOV.X	(SP,  32), R12
	MOV.Q	(SP,  48), R14
	MOV.X	(SP,  64), R24
	MOV.X	(SP,  80), R26
	MOV.X	(SP,  96), R28
	MOV.X	(SP, 112), R30
	ADD		128, SP
	RTSU

};


#endif
